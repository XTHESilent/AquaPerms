import net.fabricmc.loom.task.RemapJarTask

plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.loom)
}

archivesBaseName = 'aquaperms'

repositories {
    maven { url 'https://maven.fabricmc.net/' }
}

dependencies {
    // https://modmuss50.me/fabric.html
    minecraft 'com.mojang:minecraft:1.20.6'
    mappings 'net.fabricmc:yarn:1.20.6+build.1:v2'
    modImplementation 'net.fabricmc:fabric-loader:0.15.10'

    Set<String> apiModules = [
            'fabric-api-base',
            'fabric-command-api-v2',
            'fabric-lifecycle-events-v1',
            'fabric-networking-api-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.module(it, '0.97.8+1.20.6'))
    }

    include(modImplementation('me.lucko:fabric-permissions-api:0.3.1'))

    implementation project(':common')
}

processResources {
    inputs.property 'version', project.ext.fullVersion
    filesMatching('**/fabric.mod.json') {
        expand 'version': project.ext.fullVersion
    }
}

shadowJar {
    archiveFileName = "aquapermsfabric-${project.ext.fullVersion}-dev.jar"

    dependencies {
        exclude('net.fabricmc:.*')
        include(dependency('com.aquasplashmc:.*'))
        include(dependency('com.xthesilent.aquaperms:.*'))
        // We don't want to include the mappings in the jar do we?
        exclude '/mappings/*'
    }

    relocate 'net.kyori.adventure', 'com.xthesilent.aquaperms.lib.adventure'
    relocate 'net.kyori.event', 'com.xthesilent.aquaperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'com.xthesilent.aquaperms.lib.caffeine'
    relocate 'okio', 'com.xthesilent.aquaperms.lib.okio'
    relocate 'okhttp3', 'com.xthesilent.aquaperms.lib.okhttp3'
    relocate 'net.bytebuddy', 'com.xthesilent.aquaperms.lib.bytebuddy'
    relocate 'me.lucko.commodore', 'com.xthesilent.aquaperms.lib.commodore'
    relocate 'org.mariadb.jdbc', 'com.xthesilent.aquaperms.lib.mariadb'
    relocate 'com.mysql', 'com.xthesilent.aquaperms.lib.mysql'
    relocate 'org.postgresql', 'com.xthesilent.aquaperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'com.xthesilent.aquaperms.lib.hikari'
    relocate 'com.mongodb', 'com.xthesilent.aquaperms.lib.mongodb'
    relocate 'org.bson', 'com.xthesilent.aquaperms.lib.bson'
    relocate 'redis.clients.jedis', 'com.xthesilent.aquaperms.lib.jedis'
    relocate 'io.nats.client', 'com.xthesilent.aquaperms.lib.nats'
    relocate 'com.rabbitmq', 'com.xthesilent.aquaperms.lib.rabbitmq'
    relocate 'org.apache.commons.pool2', 'com.xthesilent.aquaperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'com.xthesilent.aquaperms.lib.configurate'
    relocate 'org.yaml.snakeyaml', 'com.xthesilent.aquaperms.lib.yaml'
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    addNestedDependencies = true
    archiveFileName = "AquaPerms-Fabric-${project.ext.fullVersion}.jar"
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}
